
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ДМ = РеквизитФормыВЗначение("ДеревоМетаданных");
	ДобавитьСправочники(ДМ);	
	ЗначениеВРеквизитФормы(ДМ,"ДеревоМетаданных");
КонецПроцедуры

функция ДобавитьВерхнююТаблицу(Дм,Картинка,ИмяТаблицы)
	ТекСтрока = ДМ.Строки.Добавить();
	ТекСтрока.Метаданное = ИмяТаблицы;
	ТекСтрока.Картинка = Картинка;
	ТекСтрока.Таблица = истина;
	ТекСтрока.ПолноеИмя = ИмяТаблицы; 
	Возврат ТекСтрока;	
КонецФункции

Процедура ДобавитьСправочники(ДМ)
	СтркаСправочникаСписок = ДобавитьВерхнююТаблицу(Дм, БиблиотекаКартинок.Справочник,"Справочник");

	
	Для Каждого Справочник Из Метаданные.Справочники Цикл
		//ПолноеИмя =   Справочник.ПолноеИмя();
		
		СтркаСправочника = ДобавитьТаблицу(СтркаСправочникаСписок,Справочник.Имя,БиблиотекаКартинок.Справочник);
		
		Для Каждого Реквизит из Справочник.СтандартныеРеквизиты Цикл
			ДобавитьРеквизит(СтркаСправочника,Реквизит.Имя,БиблиотекаКартинок.Реквизит)
		КонецЦикла;
		
		Для Каждого Реквизит из Справочник.Реквизиты Цикл
			ДобавитьРеквизит(СтркаСправочника,Реквизит.Имя,БиблиотекаКартинок.Реквизит)
		КонецЦикла;
		
		Для Каждого ОбщийРеквизит из Метаданные.ОбщиеРеквизиты Цикл
			НайденнаяСтрока = ОбщийРеквизит.Состав.Найти(Справочник);
			Если НайденнаяСтрока <> Неопределено и 
				(
				(
				НайденнаяСтрока.Использование = Метаданные.СвойстваОбъектов.ИспользованиеОбщегоРеквизита.Авто 
				и ОбщийРеквизит.АвтоИспользование = Метаданные.СвойстваОбъектов.АвтоИспользованиеОбщегоРеквизита.Использовать
				)  
				или НайденнаяСтрока.Использование = Метаданные.СвойстваОбъектов.ИспользованиеОбщегоРеквизита.Использовать
				) Тогда
				ДобавитьРеквизит(СтркаСправочника,ОбщийРеквизит.Имя,БиблиотекаКартинок.Ресурс)
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого ТабличнаяЧасть из Справочник.ТабличныеЧасти Цикл
			СтркаТЧ = ДобавитьТаблицу(СтркаСправочника,ТабличнаяЧасть.Имя,БиблиотекаКартинок.ВложеннаяТаблица);
			
			Для Каждого Реквизит из ТабличнаяЧасть.СтандартныеРеквизиты Цикл
				ДобавитьРеквизит(СтркаТЧ,Реквизит.Имя,БиблиотекаКартинок.Реквизит)
			КонецЦикла;
			Для Каждого Реквизит из ТабличнаяЧасть.Реквизиты Цикл
				ДобавитьРеквизит(СтркаТЧ,Реквизит.Имя,БиблиотекаКартинок.Реквизит)
			КонецЦикла;
		КонецЦикла;
				
		ДобавитьРеквизит(СтркаСправочника,"ВерсияДанных",БиблиотекаКартинок.Реквизит);
		ДобавитьРеквизит(СтркаСправочника,"Представление",БиблиотекаКартинок.Реквизит);
		
		ЗапросСправочник = Новый Запрос;
		ЗапросСправочник.Текст = 
		"ВЫБРАТЬ
		|	ИмяПредопределенныхДанных
		|ИЗ
		|	" + Справочник.ПолноеИмя() + "
		|ГДЕ
		|	Предопределенный";
		Выб = ЗапросСправочник.Выполнить().Выбрать();
		Пока Выб.Следующий() Цикл
			ДобавитьРеквизит(СтркаСправочника, Выб.ИмяПредопределенныхДанных,БиблиотекаКартинок.Перечисление,Истина);
		КонецЦикла;	
	КонецЦикла;
КонецПроцедуры

Функция ДобавитьРеквизит(Строка,Имя,Картинка, Значение = Ложь)
	ТекСтрока = Строка.Строки.Добавить();
	ТекСтрока.Метаданное = Имя;
	ТекСтрока.Картинка = Картинка;
	ТекСтрока.Таблица = Ложь;
	ТекСтрока.ПолноеИмя = Имя; 
	ТекСтрока.Значение = Значение;
	Возврат ТекСтрока;	
КонецФункции

Функция ДобавитьТаблицу(Строка,Имя,Картинка)
	ТекСтрока = Строка.Строки.Добавить();
	ТекСтрока.Метаданное = Имя;
	ТекСтрока.Картинка = Картинка;
	ТекСтрока.Таблица = истина;
	ТекСтрока.ПолноеИмя = ТекСтрока.Родитель.ПолноеИмя + "." + Имя; 
	Возврат ТекСтрока;	
КонецФункции